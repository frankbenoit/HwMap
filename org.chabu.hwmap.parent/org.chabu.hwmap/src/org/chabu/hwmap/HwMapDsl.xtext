grammar org.chabu.hwmap.HwMapDsl hidden(WS, ML_COMMENT, SL_COMMENT)

generate hwMapDsl "http://www.chabu.org/hwmap/HwMapDsl"

import "http://www.eclipse.org/emf/2002/Ecore" as ecore


MemoryMap:
	{MemoryMap}
	outputs+=Output*
	components+=Component*;

Output:
	{Output}
	'Output' mode=ID path=STRING	
;

Component:
	{Component}
	'Component' name=ID size=IntegerLiteral '{' 
	( blocks+=Block | insts+=Instantiation )*
	'}'	
;

Instantiation:
	{Instantiation}
	addr=IntegerLiteral type=[Block] name=ID?
;

Block:
	{Block}
	'Block' name=ID size=IntegerLiteral '{'
	(regs+=Register)*
	'}'	
;

Register:
	addr=IntegerLiteral name=ID consts+=Constant* bits+=RegisterBits*
;

RegisterBits: range=Range name=ID consts+=Constant*;

Range: '[' left=IntegerLiteral (OP_2DOT right=IntegerLiteral)? ']';

Constant: 'Constant' value=IntegerLiteral name=ID;

IntegerLiteral returns ecore::EIntegerObject: INT_DEC | INT_HEX;

terminal INT_DEC: '-'? (('1'..'9')('0'..'9')*) | '0';
terminal INT_HEX: '0x' ('a'..'f'|'A'..'F'|'0'..'9')+; 
terminal OP_2DOT: '..';


terminal ID: '^'?('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;
terminal STRING:
			'"' ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|'"') )* '"' |
			"'" ( '\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\'|"'") )* "'"
		;
terminal ML_COMMENT : '/*' -> '*/';
terminal SL_COMMENT : '//' !('\n'|'\r')* ('\r'? '\n')?;

terminal WS         : (' '|'\t'|'\r'|'\n')+;

terminal ANY_OTHER: .;